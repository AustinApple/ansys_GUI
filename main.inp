finish
/clear
/prep7
/nerr,,99999999

et,1,solid185

/input,parameter,inp

!定義各層厚度----------(5)
*dim,t_z,ARRAY,1,layer       ! define an array named t_z    shape = (1, layer)
*do,i,1,layer,1
t_z(1,%i%)=t%i%   
*enddo

!定義各層名稱----------(6)
*dim,layer_name,string,1,layer    ! define an array named layer_name   shape = (1, layer)
*do,i,1,layer,1
layer_name(1,%i%)=t_name%i%   
*enddo

!定義各層切割精度----------(7)      
*dim,mesh_z,ARRAY,1,layer         ! define an array named mesh_z   shape = (1, layer)
*do,i,1,layer,1
mesh_z(1,%i%)=t_mesh%i%   
*enddo

!----------! Total thickness
z0=0
*do,I,1,layer,1
z%I%=z%I-1%+t_z(1,%i%)
*enddo
!----------! 從Z方向建立一個1/4的平版
blc4,0,0,panel_width/2,panel_length/2,z%layer%    
!----Z方向切割----!
csys,0
wpcsys,,0
*do,i,1,layer-1,1
	wpoffs,0,0,t_z(1,%i%)       ! offsets the working plane.
	vsbw,all                    ! Subtracts intersection of the working plane from volumes (divides volumes).
*enddo
!--------Z方向線段切精細度。-------!
csys,0         ! activates a previously defined coordinate system -0 cartesian
wpcsys,,0      ! define the working plane location based on a coordinate system.
*do,i,1,layer,1   
lsel,s,length,,t_z(1,%i%)      ! selects a subset of lines.
lesize,all,,,mesh_z(1,%i%)     ! specifies the divisions and spacing ratio on unmeshed lines. # of piece
*enddo

!--------XY方向線段切精細度。-------!
lsel,s,length,,panel_width/2
lesize,all,,,panel_width/2
allsel
lsel,s,length,,panel_length/2
lesize,all,,,panel_length/2
allsel
!----------!各層命名
*do,i,1,layer,1
vsel,s,loc,z,z%i-1%,z%i%     !  selects a subset of volumes.
cm,layer_name(1,%i%),volu    !  Groups geometry items into a component.
*enddo
allsel
save

!----------!給定材料參數
*do,i,1,layer,1
mp,reft,%i%,process_%i%      ! Defines a linear material property as a constant or a function of temperature.
mp,  ex,%i%,mat_ex_%i%
mp,prxy,%i%,mat_prxy_%i%
mp,ctex,%i%,mat_ctex_%i%
*enddo

!----------!mesh
*do,i,1,layer,1
cmsel,s,layer_name(1,%i%),volu    ! Selects a subset of components and assemblies.
type,1     ! Sets the element type attribute pointer.
mat,%i%    ! Sets the element material attribute pointer.
mshape,0   ! specifies the element shape to be used for meshing.
mshkey,1   ! Specifies whether free meshing or mapped meshing should be used to mesh a model.
vmesh,all  ! Generates nodes and volume elements within volumes.
allsel
*enddo

!----------!元素命名
*do,i,1,layer,1
cmsel,s,layer_name(1,%i%),volu    ! Selects a subset of components and assemblies.
eslv,s                            ! Selects elements associated with the selected volumes.
cm,layer_name(1,%i%),elem         ! Groups geometry items into a component.
*enddo
alls
save
!----------!求解
csys,0
wpcsys,,0
nsel,s,loc,x,0
d,all,ux,0
nsel,s,loc,y,0
d,all,uy,0
nsel,s,loc,z,0
nsel,r,loc,x,0
nsel,r,loc,y,0
d,all,uz,0
allsel

*dim,process_t,ARRAY,layer,20
*dim,time_num,ARRAY,layer,20

*do,i,2,layer,1
!----------!最後一層
*IF,i,EQ,layer,THEN
t_cunt_C=NINT((process_%i%-25)/50)
process_t(%i-1%,1)=process_%i%
time_num(%i-1%,1)=%i-1%

*do,AA,1,t_cunt_C,1
*IF,process_%i%-(50*(%AA%)),LT,50,THEN
process_t(%i-1%,%AA+1%)=25
time_num(%i-1%,%AA+1%)=%(i-1)+(AA+1)/100%
*ELSE
process_t(%i-1%,%AA+1%)=process_%i%-(50*(%AA%))
time_num(%i-1%,%AA+1%)=%(i-1)+(AA+1)/100%
*ENDIF
*enddo

*ELSE
!----------!第1-N層
t_cunt_C=NINT((process_%i%-25)/50)
process_t(%i-1%,1)=process_%i%
time_num(%i-1%,1)=%i-1%

*do,AA,1,t_cunt_C,1
*IF,process_%i%-(50*(%AA%)),LT,50,THEN
process_t(%i-1%,%AA+1%)=25
time_num(%i-1%,%AA+1%)=%(i-1)+(AA+1)/100%
*ELSE
process_t(%i-1%,%AA+1%)=process_%i%-(50*(%AA%))
time_num(%i-1%,%AA+1%)=%(i-1)+(AA+1)/100%
*ENDIF
*enddo

t_cunt_H=NINT((process_%i+1%-25)/50)
t_cunt_ALL=t_cunt_C+t_cunt_H

*do,BB,t_cunt_C+1,t_cunt_ALL,1
*IF,process_t(%i-1%,%BB%)+50,GT,process_%i+1%,THEN
process_t(%i-1%,%BB+1%)=process_%i+1%
time_num(%i-1%,%BB+1%)=%(i-1)+(BB+1)/100%
*ELSE
process_t(%i-1%,%BB+1%)=process_t(%i-1%,%BB%)+50
time_num(%i-1%,%BB+1%)=%(i-1)+(BB+1)/100%
*ENDIF
*enddo

*ENDIF
*enddo

!----------!迴圈結束
/solu
outres,all,last
estif,1e-6
nropt,full
nlgeom,on
autots,on
neqit,100
EQSLV,SPARSE
antype,0
nsubst,,100000,1
kbc,0


*do,i,2,layer,1

*IF,i,EQ,layer,THEN

t_cunt_C=NINT((process_%i%-25)/50)

time,time_num(%i-1%,1)
allsel
ekill,all
esel,s,cent,z,0,z%i%
ealive,all
allsel
tunif,process_t(%i-1%,1)
solve

*do,kk,2,t_cunt_C,1
time,time_num(%i-1%,kk)
allsel
tunif,process_t(%i-1%,kk)
solve
*enddo

*ELSE

t_cunt_C=NINT((process_%i%-25)/50)
t_cunt_H=NINT((process_%i+1%-25)/50)
t_cunt_ALL=t_cunt_C+t_cunt_H

time,time_num(%i-1%,1)
allsel
ekill,all
esel,s,cent,z,0,z%i%
ealive,all
allsel
tunif,process_t(%i-1%,1)
solve

*do,kk,2,t_cunt_ALL,1
time,time_num(%i-1%,kk)
allsel
tunif,process_t(%i-1%,kk)
solve
*enddo

*ENDIF
*enddo

rescontrol,define,last,last
parsav,all,parameter,txt
finish








